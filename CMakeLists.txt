cmake_minimum_required (VERSION 3.14.5)

project (Hikari)

set (CMAKE_CXX_STANDARD 14)

############################################################################
# glm
############################################################################

include_directories (${CMAKE_CURRENT_SOURCE_DIR}/ext/glm/)

############################################################################
# nanogui
############################################################################

# Disable building extras we won't need (pure C++ project)
set (NANOGUI_BUILD_EXAMPLE OFF CACHE BOOL " " FORCE)
set (NANOGUI_BUILD_PYTHON  OFF CACHE BOOL " " FORCE)
set (NANOGUI_BUILD_SHARED  OFF CACHE BOOL " " FORCE)
set (NANOGUI_INSTALL       OFF CACHE BOOL " " FORCE)

# Add the configurations from nanogui
add_subdirectory (ext/nanogui)

# For reliability of parallel build, make the NanoGUI targets dependencies
set_property (TARGET glfw glfw_objects nanogui PROPERTY FOLDER "dependencies")

# Various preprocessor definitions have been generated by NanoGUI
add_definitions (${NANOGUI_EXTRA_DEFS})

# On top of adding the path to nanogui/include, you may need extras
include_directories (
	${CMAKE_CURRENT_SOURCE_DIR}/ext/nanogui/include
	${NANOGUI_EXTRA_INCS}
)

############################################################################
# tbb
############################################################################

# Build Thread Building Blocks (main shared libraries only)
set(TBB_BUILD_SHARED          OFF  CACHE BOOL " " FORCE)
set(TBB_BUILD_STATIC          ON  CACHE BOOL " " FORCE)
set(TBB_BUILD_TBBMALLOC       OFF CACHE BOOL " " FORCE)
set(TBB_BUILD_TBBMALLOC_PROXY OFF CACHE BOOL " " FORCE)
set(TBB_BUILD_TESTS           OFF CACHE BOOL " " FORCE)

add_subdirectory(ext/tbb)
set_property(TARGET tbb_static tbb_def_files PROPERTY FOLDER "dependencies")

include_directories (
	${CMAKE_CURRENT_SOURCE_DIR}/ext/tbb/include
)

############################################################################
# tinyobjloader
############################################################################

include_directories (
	${CMAKE_CURRENT_SOURCE_DIR}/ext/tinyobjloader
)

############################################################################
# Hikari
############################################################################

include_directories (src/)

set (HIKARI_SOURCE
	src/App.cpp
	src/BlockGenerator.cpp
	src/Camera.cpp
	src/DirectionalLight.cpp
	src/ImageBlock.cpp
	src/Interaction.cpp
	src/Main.cpp
	src/ObjLoader.cpp
	src/PointLight.cpp
	src/Primitive.cpp
	src/Ray.cpp
	src/Renderer.cpp
	src/Scene.cpp
	src/Sphere.cpp
	src/Transform.cpp
	src/Triangle.cpp
	src/Whitted.cpp
)

add_executable (Hikari ${HIKARI_SOURCE})

target_link_libraries (Hikari nanogui tbb_static ${NANOGUI_EXTRA_LIBS})